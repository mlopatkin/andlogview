/*
 * Copyright 2015 Mikhail Lopatkin
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import name.mlopatkin.andlogview.building.BuildEnvironment
import name.mlopatkin.andlogview.building.GenerateBuildMetadata
import name.mlopatkin.bitbucket.gradle.UploadTask

plugins {
    id 'java'
    id 'checkstyle'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow'
    id 'net.ltgt.errorprone'

    id 'name.mlopatkin.bitbucket'
    id 'com.diffplug.eclipse.apt'
    id 'me.champeau.gradle.jmh'
    id 'org.beryx.runtime'
}

repositories {
    mavenCentral()
    google()
}

def daggerVersion = '2.37'

dependencies {
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'log4j:log4j:1.2.17'
    implementation 'net.sf.jopt-simple:jopt-simple:4.6'
    implementation 'com.android.tools.ddms:ddmlib:26.1.3'
    implementation 'org.checkerframework:checker-qual:3.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.1'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'com.spotify:hamcrest-optional:1.1.5'
    testImplementation(platform('org.junit:junit-bom:5.6.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.vintage:junit-vintage-engine') {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }

    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    annotationProcessor 'com.uber.nullaway:nullaway:0.7.9'
    testAnnotationProcessor 'com.uber.nullaway:nullaway:0.7.9'
    jmhAnnotationProcessor 'com.uber.nullaway:nullaway:0.7.9'

    errorprone 'com.google.errorprone:error_prone_core:2.7.1'
    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
}

checkstyle {
    toolVersion = '8.12'
}

applicationName = 'andlogview'
version = '0.21' + (BuildEnvironment.isSnapshotBuild() ? '-SNAPSHOT' : '')

mainClassName = 'name.mlopatkin.andlogview.Main'

def javaVersion = JavaVersion.VERSION_1_8
def javaVersionForJPackage = JavaVersion.VERSION_16
sourceCompatibility = javaVersion
targetCompatibility = javaVersion // defaults to sourceCompatibility

// Like annotationProcessor. Note java/main instead of Gradle's usual main/java.
def metadataBuildDir = "$buildDir/generated/sources/metadata/java/main"

sourceSets {
    main {
        java {
            srcDirs = []
            srcDir 'src'
            srcDir metadataBuildDir
        }
        resources {
            srcDirs = []
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDirs = []
            srcDir 'test'
        }
        resources {
            srcDirs = []
            srcDir 'test_resources'
        }
    }
    jmh {
        java {
            srcDirs = []
            srcDir 'jmh/java'
        }
        resources {
            srcDirs = []
            srcDir 'jmh/resources'
            srcDir 'test_resources'
        }
    }
}

test {
    useJUnitPlatform()
}

task generateBuildMetadata(type: GenerateBuildMetadata) {
    packageName = 'name.mlopatkin.andlogview'
    className = 'BuildInfo'
    into = file(metadataBuildDir)
    version = project.version
}

compileJava.dependsOn(generateBuildMetadata)

idea.module.generatedSourceDirs += file(metadataBuildDir)

// Treat warnings as errors
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' << '-Werror'
    options.errorprone {
        option('NullAway:AnnotatedPackages', 'org.bitbucket.mlopatkin')
        option('NullAway:AssertsEnabled', 'true')
        option('NullAway:ExcludedClassAnnotations', 'javax.annotation.Generated')
        option('NullAway:ExcludedFieldAnnotations', 'org.checkerframework.checker.nullness.qual.MonotonicNonNull,' +
                'org.mockito.Mock,org.mockito.Captor')
        errorproneArgs.addAll(
                '-Xep:JavaLangClash:OFF',
                '-Xep:NullAway:ERROR',
                '-Xep:MissingSummary:OFF',
                '-Xep:JavaUtilDate:OFF',
                '-Xep:UnusedVariable:OFF', // Incompatible with Dagger-generated class
                '-Xep:EmptyBlockTag:OFF')
    }
}

// Configure distribution (archive creation).
distributions {
    main {
        // The application plugin resets main distribution basename to applicationName but I want to use it for shadow
        // configuration because it is the primary one. These tasks are disabled though.
        distributionBaseName = project.applicationName + '-noshadow'
    }
    shadow {
        distributionBaseName = project.applicationName
    }
}

// Using CopySpec.with(CopySpec) is the only way to add files into the distribution. Other approaches are:
// - old way of shadow.applicationDistribution no longer works after 2.0.0. The applicationDistribution from application
//   plugin still works though.
// - simply listing from/include in contents completely replaces the archive content
def additionalFiles = copySpec {
    from project.projectDir
    include 'HISTORY', 'LICENSE', 'NOTICE', 'README.md'
}

distributions.all { it.contents.with(additionalFiles) }

def disableTasks(String... taskNames) {
    for (String taskName : taskNames) {
        tasks.named(taskName).configure {
            it.enabled = false
        }
    }
}

// Disable tasks from application plugin
disableTasks(
        ApplicationPlugin.TASK_DIST_ZIP_NAME,
        ApplicationPlugin.TASK_DIST_TAR_NAME,
        ApplicationPlugin.TASK_START_SCRIPTS_NAME,
        'assembleDist',
        'installDist')

// TARs aren't shipped, so disable it
disableTasks('shadowDistTar')
// Modern gradle support in eclipse (Buildship) doesn't require this task but it is brought in by eclipse.apt plugin
disableTasks('eclipse')

// Tweak windows start script so it won't create console window.
// Based on https://stackoverflow.com/a/27472895/200754
// and http://mrhaki.blogspot.ru/2015/04/gradle-goodness-alter-start-scripts.html
startShadowScripts {
    def replaceJavaWithJavaW = {
        if (it.contains("java.exe")) {
            return it.replaceAll("java.exe", "javaw.exe")
        } else if (it.startsWith("\"%JAVA_EXE%\"")) {
            return "start \"\" /b " + it
        }
        return it
    }
    doLast {
        windowsScript.text = windowsScript
                .readLines()
                .collect(replaceJavaWithJavaW)
                .join('\r\n')
    }
}

shadowJar {
    // NOTICES and licenses are included into ours NOTICE file manually.
    // Maven metadata isn't necessary also.
    exclude '**/NOTICE'
    exclude '**/LICENSE'
    exclude 'META-INF/maven/**'

    // These annotations aren't needed in runtime
    dependencies {
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('org.checkerframework:checker-qual'))
        exclude(dependency('com.google.errorprone:error_prone_annotations'))
        exclude(dependency('com.google.j2objc:j2objc-annotations'))
        exclude(dependency('org.codehaus.mojo:animal-sniffer-annotations'))
    }
}

bitbucket {
    repository = 'android-log-viewer'
    username = project.hasProperty('bitbucket.user') ? project.property('bitbucket.user') : System.getenv('BITBUCKET_USER')
    applicationPassword = project.hasProperty('bitbucket.password') ? project.property('bitbucket.password') : System.getenv('BITBUCKET_PASSWORD')
}

tasks.register('bitbucketUpload', UploadTask) {
    fileToUpload.fileProvider tasks.named('shadowDistZip').map { it.archivePath }
}

jmh {
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps false
    reproducibleFileOrder true
}

// Order of tasks is important or generated metadata sources will not be imported
eclipse.synchronizationTasks 'generateBuildMetadata', 'eclipseJdtApt', 'eclipseFactorypath', 'eclipseJdt'

if (System.getProperty('os.name').toLowerCase(Locale.US).contains("linux")) {
    def jpackageJdkPath = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_16.majorVersion)
    }.get().metadata.installationPath.toString()

    runtime {
        options = [
                '--strip-debug',
                '--no-header-files',
                '--no-man-pages',
                '--strip-native-commands',
                '--ignore-signing-information'
        ]
        javaHome = jpackageJdkPath
        jpackage {
            jpackageHome = jpackageJdkPath
            installerType = 'deb'
            installerOptions = ['--linux-shortcut']
            resourceDir = file('install/debian')
        }
    }
}
